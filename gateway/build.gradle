group 'iimetra.cloud.editor'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/libs-snapshot' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Edgware.RELEASE'
    }
}

task npmInstall(type:Exec) {
    workingDir 'src/main/react/dashboard'
    commandLine 'npm', 'install'
}

task webpack(type:Exec, dependsOn:npmInstall) {
    workingDir 'src/main/react/dashboard'
    commandLine 'npm', 'run', 'build'
}

task processStatic(type:Copy, dependsOn: webpack) {
    from 'src/main/react/dashboard/build'
    into 'src/main/resources/static'
}

tasks.idea {
    dependsOn = [cleanIdeaModule, ideaModule, processStatic]
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-config'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-eureka'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-zuul'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-feign'
    //compile group: 'org.springframework.boot', name: 'spring-boot-devtools'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    testCompile group: 'junit', name: 'junit'
}

